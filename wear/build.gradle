apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "com.adamshort.canieatthis.app"
        minSdkVersion 20
        targetSdkVersion 24

        // Fetch the version according to git latest tag and "how far are we from last tag"
        def longVersionName = "git -C ${rootDir} describe --tags".execute().text.trim()
        def (fullVersionTag, gitSha) = longVersionName.tokenize('-')
        def (versionMajor, versionMinor, versionPatch) = fullVersionTag.tokenize('.')

        // Set the version name
        versionName "$versionMajor.$versionMinor.$versionPatch"

        // Turn the version name into a version code
        versionCode versionMajor.toInteger() * 100 +
                versionMinor.toInteger() * 10 +
                versionPatch.toInteger()

        // Friendly print the version output to the Gradle console
        printf("\n--------" + "VERSION DATA--------" + "\n" + "- CODE: " + versionCode + "\n" +
                "- NAME: " + versionName + "\n----------------------------\n")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.google.android.support:wearable:2.0.0-alpha2'
    compile 'com.google.android.gms:play-services-wearable:9.4.0'
    compile 'com.google.android.gms:play-services-maps:9.4.0'
    provided 'com.google.android.wearable:wearable:1.0.0'
    compile 'com.google.android.gms:play-services-location:9.4.0'
    compile project(':devicebridge')
}
